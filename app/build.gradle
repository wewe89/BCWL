apply plugin: 'com.android.application'

//定义打包时间函数
def packageTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion Integer.parseInt(ANDROID_BUILD_COMPILE_SDK_VERSION)
    buildToolsVersion ANDROID_BUILD_TOOLS_VERSION
    compileSdkVersion 22
    buildToolsVersion "22.0.1"

    defaultConfig {
        applicationId "cn.appem.bcwl"

        minSdkVersion Integer.parseInt(MIN_SDK_VERSION)
        targetSdkVersion Integer.parseInt(ANDROID_BUILD_TARGET_SDK_VERSION)
        versionCode Integer.parseInt(VERSION_CODE)
        versionName VERSION_NAME

        sourceSets {
            main {
                jniLibs.srcDirs = ['libs']
            }
        }
    }

    signingConfigs {
        release {
            def Properties keyProps = new Properties()
            keyProps.load(new FileInputStream(file('/sign.properties')))

            storeFile file(keyProps["STORE_FILE"])
            storePassword keyProps["STORE_PASSWORD"]
            keyAlias keyProps["KEY_ALIAS"]
            keyPassword keyProps["KEY_PASSWORD"]
        }
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"//是否输出LOG信息
            buildConfigField "String", "API_HOST", "\"http://api.test.com\""//API Host
            minifyEnabled false
            zipAlignEnabled true
            shrinkResources true

            signingConfig signingConfigs.release
        }
        release {
            buildConfigField "boolean", "LOG_DEBUG", "false"//是否输出LOG信息
            buildConfigField "String", "API_HOST", "\"http://api.release.com\""//API Host
            minifyEnabled false//混淆编译
            zipAlignEnabled true
            //移除无用的资源文件
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            signingConfig signingConfigs.release
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    // 移除lint检查的error
    lintOptions {
        disable 'InvalidPackage'
        abortOnError false
    }

    //修改生成的最终文件名
    applicationVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.apk')) {
                File outputDirectory = new File(outputFile.parent);
                def fileName
                if (variant.buildType.name == "release") {
                    // 输出apk名称为app_v1.0.0_2015-06-15_playStore.apk
//                    fileName = "app_v${defaultConfig.versionName}_${packageTime()}_${variant.productFlavors[0].name}.apk"
                    fileName = "app_v${defaultConfig.versionName}_release.apk"
                } else {
//                    fileName = "app_v${defaultConfig.versionName}_${packageTime()}_beta.apk"
                    fileName = "app_v${defaultConfig.versionName}_debug.apk"
                }
                output.outputFile = new File(outputDirectory, fileName)
            }
        }
    }

    productFlavors {
        playStore {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "playStore"]
        }
        miui {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "miui"]
        }
        wandoujia {
            manifestPlaceholders = [UMENG_CHANNEL_VALUE: "wandoujia"]
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.android.support:appcompat-v7:22.2.0'
    compile 'com.android.support:recyclerview-v7:22.2.0'
    compile 'com.android.support:cardview-v7:22.2.0'

//    compile 'com.mcxiaoke.volley:library:1.0.+'
    compile 'com.loopj.android:android-async-http:1.4.6'
//    compile 'com.umeng:fb:5.3.0'
    compile 'com.umeng.analytics:analytics:latest.integration'
    compile 'com.readystatesoftware.systembartint:systembartint:1.0.3'
    compile 'com.jakewharton:butterknife:5.1.1'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.3'
    compile project(':ChatDemoUI')
//    compile project(':framework')
//    compile project(':umeng_fb')
    compile project(':social_sdk_library_project')
}
repositories {
    maven { url "https://raw.githubusercontent.com/umeng/mvn-repo-umeng/master/repository" }
}